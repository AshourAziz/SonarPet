buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'

description = 'SonarPet'
dependencies {
    compile project(':core')
}

shadowJar {
    baseName = "SonarPet"
    classifier = null
    version = null
    configurations = [project.configurations.compile]
    dependencies {
        /*
         * Only include sonarpet and ASM in the shaded jar.
         * All other dependencies are dynamically loaded at runtime.
         * We can't use ASM because our (updated) version of ASM conflicts with Paper's.
         */
        exclude { it.moduleGroup != "net.techcable.sonarpet" && it.moduleGroup != "org.ow2.asm" }
        // Relocate ASM so it doesn't conflict
        relocate 'org.objectweb.asm', 'net.techcable.sonarpet.libs.asm'
    }
}

processResources {
    filesMatching("plugin.yml") {
        filter {
            it.replace("\${project.version}", project.version.toString())
        }
    }
}

build.dependsOn(shadowJar)
