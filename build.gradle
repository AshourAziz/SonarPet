plugins {
    id "java"
    id "maven"
    id "org.jetbrains.kotlin.jvm" version "1.1.2"
    id "com.github.johnrengelman.shadow" version "1.2.4" apply false
}

allprojects {
    apply plugin: 'maven'

    group = 'net.techcable.sonarpet'
    version = '1.1.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    configurations {
        shade {
            description = "Shade into the output jar"
            transitive = true
        }
        compile {
            extendsFrom shade
        }
    }
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.warnings = false
    }
    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    test {
        useJUnit {
            maxParallelForks = 4
            if (!hasProperty("runSlowTests") && System.getProperty("user.name") != "jenkins") {
                excludeCategories 'net.techcable.sonarpet.test.SlowTest'
            }
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name "techcable-repo"
            url "https://repo.techcable.net/content/groups/public/"
        }
        maven { url "https://hub.spigotmc.org/nexus/content/groups/public/" }
        maven { url "http://repo.dmulloy2.net/content/groups/public/" }
        maven { url "http://repo.md-5.net/content/groups/public/" }
        maven { url "http://ci.hawkfalcon.com/plugin/repository/everything/" }
        maven { url "http://maven.sk89q.com/repo/" }
        maven { url "http://repo.kitteh.org/content/groups/public" }
        maven { url "http://nexus.theyeticave.net/content/repositories/pub_releases" }
        maven { url "http://maven.elmakers.com/repository/" }
    }


    dependencies {
        testCompile "junit:junit:4.12"
        testCompile "com.googlecode.junit-toolbox:junit-toolbox:2.3"
        compile "net.techcable:pineapple:0.1.0-beta5"
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:1.1.2"
        compile("com.dsh105:Commodus:1.0.5") {
            exclude(module: 'Minecraft-Reflection')
        }
        compile("com.dsh105:PowerMessage:1.0.1-SNAPSHOT") {
            exclude(module: 'Commodus')
        }
        compile "org.slf4j:slf4j-jdk14:1.7.5"
        compile "com.zaxxer:HikariCP:2.4.5"
        /*
         * An outdated version of ASM is used by Paper.
         * We need to shade in our (modern) version,
         * or else we'll use Paper's outdated version and crash.
         */
        shade "org.ow2.asm:asm:5.2"
        shade "org.ow2.asm:asm-commons:5.2"
        shade "org.ow2.asm:asm-util:5.2"
        // Provided dependencies
        compileOnly "org.bukkit:bukkit:1.11.2-R0.1-SNAPSHOT"
        compileOnly("org.projectlombok:lombok:1.16.12")
        compileOnly("org.kitteh:VanishNoPacket:3.18.7") {
            exclude(module: 'Vault')
        }
        compileOnly "com.sk89q:worldedit:6.0.0-SNAPSHOT"
        compileOnly "com.sk89q:worldguard:6.0.0-SNAPSHOT"
        compileOnly "com.comphenix.protocol:ProtocolLib:3.6.5-SNAPSHOT"
    }
    // All test modules depend on the API's test module
    if (name != 'api') {
        dependencies {
            //noinspection GroovyAssignabilityCheck - It works fine ^_^
            testCompile files(project(":api").sourceSets.test.output)
        }
    }
}
